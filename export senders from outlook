tell application "Microsoft Outlook"
	set emailList to {} -- to save all senders
	set allMessages to messages of inbox -- access messages from "inbox"
	if (count of allMessages) is greater than 0 then
		repeat with msg in allMessages
			try
				-- access sender's info
				set senderInfo to sender of msg
				if senderInfo is not missing value then
					try
						-- access sender's email
						set senderEmail to address of senderInfo
						-- add email to emailList if not in list
						if senderEmail is not in emailList then
							set end of emailList to senderEmail
						end if
					on error
						set end of emailList to "Error: No address available"
					end try
				else
					set end of emailList to "Error: No sender info"
				end if
			on error errMsg
				set end of emailList to "Error: " & errMsg
			end try
		end repeat
	else
		set emailList to {"No messages found in inbox"}
	end if
	-- join email to emailList with comma
	set textResult to my join_with_comma(emailList)
end tell

-- function
on join_with_comma(theList)
	set textResult to ""
	repeat with anItem in theList
		set textResult to textResult & anItem & ", "
	end repeat
	-- extract extra space and comma
	if (length of textResult) > 0 then
		set textResult to text 1 through -3 of textResult
	end if
	return textResult
end join_with_comma
